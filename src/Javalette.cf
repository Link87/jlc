-- programs ------------------------------------------------

entrypoints Prog ;

Program.    Prog ::= [TopDef] ;

FnDef.	    TopDef ::= Type Ident "(" [Arg] ")" Blk ;

ClsDef.	    TopDef ::= "class" Ident "{" [ClsElem] "}" ;

SubClsDef.  TopDef ::= "class" Ident "extends" Ident "{" [ClsElem] "}" ;

separator nonempty TopDef "" ;

Argument.   Arg ::= Type Ident;

separator   Arg "," ;

InstVar.    ClsElem ::= Type Ident ";" ;

MethDef.    ClsElem ::= Type Ident "(" [Arg] ")" Blk ;

separator   ClsElem "" ;

-- statements ----------------------------------------------

Block.      Blk ::= "{" [Stmt] "}" ;

separator   Stmt "" ;

Empty.      Stmt ::= ";" ;

BStmt.      Stmt ::= Blk ;

Decl.       Stmt ::= Type [DeclItem] ";" ;

NoInit.     DeclItem ::= Ident ;

Init.       DeclItem ::= Ident "=" Expr ;

separator nonempty DeclItem "," ;

Ass.        Stmt ::= Expr "=" Expr ";" ;

Incr.       Stmt ::= Expr "++" ";" ;

Decr.       Stmt ::= Expr "--" ";" ;

Ret.        Stmt ::= "return" Expr ";" ;

VRet.       Stmt ::= "return" ";" ;

Cond.       Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.   Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.      Stmt ::= "while" "(" Expr ")" Stmt ;

ForEach.    Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;

SExpr.      Stmt ::= Expr  ";" ;

-- L-Values --------------------------------------------------

internal ArrId. LValue ::= LValue "[" Expr "]" ;

internal Id.    LValue ::= Ident ;

-- Types ---------------------------------------------------

Int.        Type ::= "int" ;

Double.     Type ::= "double" ;

Boolean.    Type ::= "boolean" ;

Void.       Type ::= "void" ;

Array.      Type ::= Type "[]";

Object.     Type ::= Ident ;

separator   Type "," ;

internal Fn.        Type ::= Type "(" [Type] ")" ;

internal String.    Type ::= "String" ;


-- Expressions ---------------------------------------------

EVar.       Expr9 ::= Ident ;

ELitInt.    Expr9 ::= Integer ;

ELitDoub.   Expr9 ::= Double;

ELitTrue.   Expr9 ::= "true" ;

ELitFalse.  Expr9 ::= "false" ;

ENull.      Expr9 ::= "(" Ident ")null" ;

ECall.      Expr9 ::= Ident "(" [Expr] ")" ;

EString.    Expr9 ::= String ;

ENew.       Expr8 ::= "new" Type [SizeItem] ;

SizeSpec.   SizeItem ::= "[" Expr "]" ;

separator   SizeItem "" ;

EArrIndex.  Expr8 ::= Expr8 "[" Expr "]" ;

EDot.       Expr7 ::= Expr7 "." Expr8 ;

ENeg.       Expr5 ::= "-" Expr6 ;

ENot.       Expr5 ::= "!" Expr6 ;

EMul.       Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.       Expr3 ::= Expr3 AddOp Expr4 ;

ERel.       Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.       Expr1 ::= Expr2 "&&" Expr1 ;

EOr.        Expr ::= Expr1 "||" Expr ;

coercions   Expr 9 ;

separator   Expr "," ;

internal ETyped.    Expr ::= "[" Expr ":" Type "]" ;

internal ELValue.   Expr ::= LValue ;

internal EObjInit.  Expr ::= "new" Type ;

internal EArrAlloc. Expr ::= "new" Type [SizeItem];

internal EMethCall. Expr ::= Expr "." Ident "(" [Expr] ")" ;

internal EArrLen.   Expr ::= Expr "." "length" ;

-- operators -----------------------------------------------

Plus.       AddOp ::= "+" ;

Minus.      AddOp ::= "-" ;

Times.      MulOp ::= "*" ;

Div.        MulOp ::= "/" ;

Mod.        MulOp ::= "%" ;

LTH.        RelOp ::= "<" ;

LE.         RelOp ::= "<=" ;

GTH.        RelOp ::= ">" ;

GE.         RelOp ::= ">=" ;

EQU.        RelOp ::= "==" ;

NE.         RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment     "#" ;

comment     "//" ;

comment     "/*" "*/" ;

